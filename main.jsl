NamesDefaultToHere(1);

gdd = GetDefaultDirectory();
If (Hostis("Windows"),
    gdd = ConvertFilePath(gdd, windows);
);

win = NewWindow("Python Setup Interface",
    ShowMenu(0), ShowToolbars(0),
	tb = TabBox(
		TabPageBox(
			Title("Setup"),
			VListBox(
				SpacerBox(),
				HListBox(
					SpacerBox(),
					tbProcess = TextBox("Prepare",
						<< JustifyText("center");
					),
					SpacerBox()
				),
				SpacerBox(),
				HListBox(
					SpacerBox(),
					btnStart = ButtonBox("Start",
						PythonSubmitFile(gdd || "scripts/setup.py");
					),
					SpacerBox()
				),
				SpacerBox()
			)
		),
		TabPageBox(
			Title("Requirements"),
			HListBox(
				SpacerBox(),
				VListBox(
					SpacerBox(),
					ButtonBox("Load File...",
						requireFile = PickFile(
							"Select requirements file",
							"$DOCUMENTS",
							{ "Requirements File|txt" }
						);

						If(requireFile != "",
							PythonSend(requireFile);
							PythonSubmitFile(gdd || "scripts/load_require_file.py");
							sbList << SetText(PythonGet("requireList"));
						);
					),
					HListBox(
						sbList = ScriptBox(
							"# list of requirements\!N",
							"Text", 350, 250
						)
					),
					SpacerBox(),
					HListBox(
						SpacerBox(),
						VListBox(
							SpacerBox(),
							tbRequireProgress = TextBox("",
								<< JustifyText("right");
							),
							SpacerBox()
						),
						ButtonBox("Install",
                            reqList = sbList << GetText();

							PythonSend(gdd);
							PythonSend(reqList);
							PythonSubmitFile(gdd || "scripts/install_requires.py");
						)
					),
					SpacerBox()
				)
			)
		)
	),
	<< SetWindowSize(400, 300);
);
//win << SetWindowSize(400, 200);
